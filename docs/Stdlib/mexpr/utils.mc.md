import { DocBlock, Signature, Description, ToggleWrapper, S} from '@site/docs/Stdlib/MikingDocGen';
import Search from '@site/docs/Stdlib/Search';

<Search />
# utils.mc  
  

Simple utility functions defined on MExpr ASTs.

  
  
## Stdlib Includes  
  
<a href={"/docs/Stdlib/map.mc"} style={S.link}>map.mc</a>, <a href={"/docs/Stdlib/string.mc"} style={S.link}>string.mc</a>, <a href={"/docs/Stdlib/mexpr/ast.mc"} style={S.link}>mexpr/ast.mc</a>, <a href={"/docs/Stdlib/mexpr/boot-parser.mc"} style={S.link}>mexpr/boot-parser.mc</a>, <a href={"/docs/Stdlib/mexpr/symbolize.mc"} style={S.link}>mexpr/symbolize.mc</a>  
  
## Languages  
  

          <DocBlock title="MExprSubstitute" kind="lang" link="/docs/Stdlib/mexpr/utils.mc/lang-MExprSubstitute">

```mc
lang MExprSubstitute
```



<ToggleWrapper text="Code..">
```mc
lang MExprSubstitute = MExprAst
  -- Applies the substitutions of the provided map to the identifiers of the
  -- given AST.
  sem substituteIdentifiers : Map Name Name -> Expr -> Expr
  sem substituteIdentifiers replacements =
  | ast -> substituteIdentifiersExpr replacements ast

  sem subIdent : Map Name Name -> Name -> Name
  sem subIdent replacements =
  | id -> optionGetOrElse (lam. id) (mapLookup id replacements)

  sem substituteIdentifiersSDecl : Map Name Name -> Decl -> Decl
  sem substituteIdentifiersSDecl replacements =
  | d -> d
  | DeclLet x -> DeclLet {x with ident = subIdent replacements x.ident}
  | DeclRecLets x ->
    let f = lam bind. {bind with ident = subIdent replacements bind.ident} in
    DeclRecLets {x with bindings = map f x.bindings}
  | DeclType x -> DeclType
    { x with ident = subIdent replacements x.ident
    , params = map (subIdent replacements) x.params
    }
  | DeclConDef x -> DeclConDef {x with ident = subIdent replacements x.ident}
  | DeclExt x -> DeclExt {x with ident = subIdent replacements x.ident}

  sem substituteIdentifiersExpr : Map Name Name -> Expr -> Expr
  sem substituteIdentifiersExpr replacements =
  | TmVar t -> TmVar {t with ident = subIdent replacements t.ident}
  | TmConApp t ->
    let ast = TmConApp {t with ident = subIdent replacements t.ident} in
    let ast = smap_Expr_Expr (substituteIdentifiersExpr replacements) ast in
    let ast = smap_Expr_Type (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_TypeLabel (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_Pat (substituteIdentifiersPat replacements) ast in
    withType (substituteIdentifiersType replacements (tyTm ast)) ast
  | TmLam t ->
    let ast = TmLam {t with ident = subIdent replacements t.ident} in
    let ast = smap_Expr_Expr (substituteIdentifiersExpr replacements) ast in
    let ast = smap_Expr_Type (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_TypeLabel (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_Pat (substituteIdentifiersPat replacements) ast in
    withType (substituteIdentifiersType replacements (tyTm ast)) ast
  | TmDecl x ->
    let ast = TmDecl {x with decl = substituteIdentifiersSDecl replacements x.decl} in
    let ast = smap_Expr_Expr (substituteIdentifiersExpr replacements) ast in
    let ast = smap_Expr_Type (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_TypeLabel (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_Pat (substituteIdentifiersPat replacements) ast in
    withType (substituteIdentifiersType replacements (tyTm ast)) ast
  | ast ->
    let ast = smap_Expr_Expr (substituteIdentifiersExpr replacements) ast in
    let ast = smap_Expr_Type (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_TypeLabel (substituteIdentifiersType replacements) ast in
    let ast = smap_Expr_Pat (substituteIdentifiersPat replacements) ast in
    withType (substituteIdentifiersType replacements (tyTm ast)) ast

  sem substituteIdentifiersType : Map Name Name -> Type -> Type
  sem substituteIdentifiersType replacements =
  | TyCon t -> TyCon {t with ident = subIdent replacements t.ident}
  | TyVar t -> TyVar {t with ident = subIdent replacements t.ident}
  | TyAll t -> TyAll {t with ident = subIdent replacements t.ident, ty = substituteIdentifiersType replacements t.ty}
  | ty -> smap_Type_Type (substituteIdentifiersType replacements) ty

  sem substituteIdentifiersPat : Map Name Name -> Pat -> Pat
  sem substituteIdentifiersPat replacements =
  | PatCon t ->
    PatCon {t with ident = subIdent replacements t.ident,
                   subpat = substituteIdentifiersPat replacements t.subpat}
  | p ->
    let p = smap_Pat_Pat (substituteIdentifiersPat replacements) p in
    withTypePat (substituteIdentifiersType replacements (tyPat p)) p
end
```
</ToggleWrapper>
</DocBlock>


          <DocBlock title="MExprFindSym" kind="lang" link="/docs/Stdlib/mexpr/utils.mc/lang-MExprFindSym">

```mc
lang MExprFindSym
```



<ToggleWrapper text="Code..">
```mc
lang MExprFindSym = MExprAst
  -- Finds the names corresponding to a provided sequence of strings in a given
  -- AST. If 'id' is the first bound name matching a provided string, then
  -- 'Some id' is the result corresponding to that input string. If no name is
  -- found for a provided string, 'None' is the corresponding result for that
  -- string.
  --
  -- The function assumes the provided sequence of strings are distinct.
  sem findNamesOfStrings : [String] -> Expr -> [Option Name]
  sem findNamesOfStrings strs =
  | t ->
    let result : Map Int Name =
      findNamesOfStringsExpr
        (mapFromSeq cmpString (mapi (lam i. lam x. (x, i)) strs))
        (mapEmpty subi) t in
    create (length strs) (lam i. mapLookup i result)

  sem findNamesOfStringsExn : [String] -> Expr -> [Name]
  sem findNamesOfStringsExn strs =
  | t ->
    let r = findNamesOfStrings strs t in
    map (lam n. match n with Some n then n else error (join ["Couldn't find name"])) r

  sem findName : String -> Expr -> Option Name
  sem findName str =
  | t ->
    match findNamesOfStrings [str] t with [Some id] then
      Some id
    else None ()

  sem findNamesOfStringsExpr : Map String Int -> Map Int Name -> Expr -> Map Int Name
  sem findNamesOfStringsExpr strs acc =
  | TmDecl (x & {decl = DeclLet t}) ->
    let acc = checkIdentifier strs acc t.ident in
    let acc = findNamesOfStringsExpr strs acc t.body in
    findNamesOfStringsExpr strs acc x.inexpr
  | TmDecl (x & {decl = DeclRecLets t}) ->
    let findNamesBinding = lam acc. lam binding.
      checkIdentifier strs acc binding.ident
    in
    let findNamesBindingBody = lam acc. lam binding.
      findNamesOfStringsExpr strs acc binding.body
    in
    let acc = foldl findNamesBinding acc t.bindings in
    let acc = foldl findNamesBindingBody acc t.bindings in
    findNamesOfStringsExpr strs acc x.inexpr
  | TmDecl (x & {decl
    = DeclType {ident = ident, tyIdent = tyIdent}
    | DeclConDef {ident = ident, tyIdent = tyIdent}
    | DeclExt {ident = ident, tyIdent = tyIdent}
    }) ->
    let acc = checkIdentifier strs acc ident in
    let acc = findNamesOfStringsType strs acc tyIdent in
    findNamesOfStringsExpr strs acc x.inexpr
  | t -> sfold_Expr_Expr (findNamesOfStringsExpr strs) acc t

  sem findNamesOfStringsType : Map String Int -> Map Int Name -> Type -> Map Int Name
  sem findNamesOfStringsType strs acc =
  | TyCon {ident = ident} | TyVar {ident = ident} ->
    checkIdentifier strs acc ident
  | ty -> sfold_Type_Type (findNamesOfStringsType strs) acc ty

  sem checkIdentifier : Map String Int -> Map Int Name -> Name -> Map Int Name
  sem checkIdentifier strs acc =
  | id ->
    match mapLookup (nameGetStr id) strs with Some index then
      if mapMem index acc then acc
      else mapInsert index id acc
    else acc
end
```
</ToggleWrapper>
</DocBlock>


          <DocBlock title="TestLang" kind="lang" link="/docs/Stdlib/mexpr/utils.mc/lang-TestLang">

```mc
lang TestLang
```



<ToggleWrapper text="Code..">
```mc
lang TestLang =
  MExprFindSym + MExprSubstitute + BootParser + MExprSym + MExprPrettyPrint
end
```
</ToggleWrapper>
</DocBlock>

## Mexpr  
  

          <DocBlock title="mexpr" kind="mexpr">

```mc
mexpr
```



<ToggleWrapper text="Code..">
```mc
mexpr

use TestLang in

let pp = lam e. mexprToString e in

let expr = lam id. bindall_ [
  ulet_ id (ulam_ id (var_ id)),
  ureclets_ [(id, var_ id)],
  type_ id [] (tyapp_ (tycon_ id) tyint_),
  condef_ id (tyarrow_ tyint_ (tycon_ id)),
  ext_ id false tyunknown_]
  (conapp_ id (int_ 2))
in
let replace = mapFromSeq nameCmp [(nameNoSym "x", nameNoSym "y")] in
utest pp (substituteIdentifiers replace (expr "x")) with pp (expr "y") using eqString in

let parseProgram : String -> Expr =
  lam str.
  let parseArgs = {defaultBootParserParseMExprStringArg with allowFree = true} in
  let ast = parseMExprStringExn parseArgs str in
  symbolizeAllowFree ast
in

let matchOpt : all a. all b. Option a -> Option b -> Bool =
  lam o1. lam o2.
  match (o1, o2) with (Some _, Some _) then true
  else match (o1, o2) with (None _, None _) then true
  else false
in
recursive let matchOpts : all a. all b. [Option a] -> [Option b] -> Bool =
  lam opts1. lam opts2.
  match (opts1, opts2) with ([o1] ++ opts1, [o2] ++ opts2) then
    and (matchOpt o1 o2) (matchOpts opts1 opts2)
  else match (opts1, opts2) with ([], []) then true
  else error "Inconsistent lengths of arguments"
in

let t = parseProgram "let foo = lam. 42 in ()" in
utest findNamesOfStrings ["foo"] t with [Some ()] using matchOpts in

let t = parseProgram "recursive let foo = lam. 42 in ()" in
utest findNamesOfStrings ["foo"] t with [Some ()] using matchOpts in

let t = parseProgram "external foo : () in ()" in
utest findNamesOfStrings ["foo"] t with [Some ()] using matchOpts in

-- NOTE(larshum, 2022-09-13): This program was constructed based on the current
-- shape of the utest runtime.
let s = "
type Option a in
con Some : all a. a -> Option a in
con None : all a. () -> Option a in

let numFailed = ref 0 in
let join = lam seqs.
  foldl concat [] seqs
in
let printLn = lam s.
  print (concat s \"\\n\")
in
recursive
  let strJoin = lam delim. lam strs.
    if eqi (length strs) 0
    then \"\"
    else if eqi (length strs) 1
         then head strs
         else concat (concat (head strs) delim) (strJoin delim (tail strs))
in
let utestTestPassed = lam.
  print \".\"
in
let utestRunner =
  lam info     : String.
  lam usingStr : String.
  lam lpprint  : Unknown -> String.
  lam rpprint  : Unknown -> String.
  lam eqfunc   : Unknown -> Unknown -> Bool.
  lam lhs      : Unknown.
  lam rhs      : Unknown.
  -- Comparison
  if eqfunc lhs rhs then
    utestTestPassed ()
  else
    utestTestFailed info (lpprint lhs) (rpprint rhs) usingStr
in
()" in
let prog = parseProgram s in

-- NOTE(larshum, 2022-09-13): We verify that the expected identifiers are found
-- in the program. However, we do not test the symbols themselves as we cannot
-- know these beforehand.
let strs = ["Option", "Error", "Some", "in", "utestRunner", "numFailed", "eqExpr"] in
let expected = [Some (), None (), Some (), None (), Some (), Some (), None ()] in
utest findNamesOfStrings strs prog with expected using matchOpts in

()
```
</ToggleWrapper>
</DocBlock>

