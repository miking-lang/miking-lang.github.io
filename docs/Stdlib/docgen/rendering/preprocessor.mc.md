import { DocBlock, Signature, Description, ToggleWrapper, S} from '@site/docs/Stdlib/MikingDocGen';
import Search from '@site/docs/Stdlib/Search';

<Search />
# preprocessor.mc  
  

\# Preprocess step: create output directories  
  
This module implements the preprocessing step:  
\- Walks the ObjectTree  
\- Computes all the output directories needed to generate doc files  
\- Creates them using \`mkdir \-p\`  
  
It builds a PathMap and runs a system command at the end.  
The preprocessor have to use the NameContext too generate all the folders correctly.

  
  
## Stdlib Includes  
  
<a href={"/docs/Stdlib/docgen/extracting/objects.mc"} style={S.link}>../extracting/objects.mc</a>, <a href={"/docs/Stdlib/docgen/rendering/renderers/objects-renderer.mc"} style={S.link}>./renderers/objects-renderer.mc</a>, <a href={"/docs/Stdlib/docgen/global/util.mc"} style={S.link}>../global/util.mc</a>, <a href={"/docs/Stdlib/fileutils.mc"} style={S.link}>fileutils.mc</a>, <a href={"/docs/Stdlib/hashmap.mc"} style={S.link}>hashmap.mc</a>, <a href={"/docs/Stdlib/docgen/options/docgen-options.mc"} style={S.link}>../options/docgen-options.mc</a>, <a href={"/docs/Stdlib/docgen/global/format.mc"} style={S.link}>../global/format.mc</a>  
  
## Variables  
  

          <DocBlock title="preprocess" kind="let">

```mc
let preprocess obj opt : ObjectTree -> RenderingOptions -> ()
```



<ToggleWrapper text="Code..">
```mc
let preprocess : ObjectTree -> RenderingOptions -> () = use ObjectsRenderer in lam obj. lam opt.
    -- Map of all output paths (acts as a Set)
    type PathMap = HashMap String () in
    -- Recursively visit the ObjectTree and collect paths
    recursive let preprocessRec : PathMap -> ObjectTree -> PathMap = use ObjectKinds in
        lam pathMap. lam obj.
        let inner = objTreeObj obj in
        let nameContext =
            match objNameIfHas inner with Some name then
             hmInsert name (objGetPureLink inner opt) opt.nameContext
            else opt.nameContext
        in
        
        let opt = { opt with nameContext = nameContext } in
        switch obj
        case ObjectNode { obj = { kind = ObjInclude {} } & obj, sons = [ p ] } then
            if and (objIsStdlib obj) opt.noStdlib then pathMap else
                preprocessRec pathMap p
        case ObjectNode { obj = { kind = ObjRecursiveBloc {} }, sons = sons } then
            foldl preprocessRec pathMap sons
        case ObjectNode { obj = obj, sons = sons } then
            let path = dirname (join [opt.outputFolder, objLink obj opt]) in
            let map = hmInsert path () pathMap in
            if objRenderIt obj then
                foldl preprocessRec map sons
            else pathMap            
        case _ then pathMap
        end

    in
    let pathMap = preprocessRec (hashmapEmpty ()) obj in
    recursive let create = lam arr.
        let batchSize = 1000 in
        match arr with [] then ()
        else
            let arr = if lti (length arr) batchSize then (arr, []) else splitAt arr batchSize in
            let command = concat ["mkdir", "-p"] arr.0 in
            let res = sysRunCommand command "" "." in
            match res.returncode with 0 then create arr.1 else error "Failed to create folders during preprocessing"
    in create (hmKeys pathMap)
```
</ToggleWrapper>
</DocBlock>

